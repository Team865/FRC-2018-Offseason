import edu.wpi.first.gradlerio.GradleRIOPlugin
import edu.wpi.first.gradlerio.frc.FRCJavaArtifact
import edu.wpi.first.gradlerio.frc.RoboRIO

plugins {
    id "java"
    id "edu.wpi.first.GradleRIO"
}

repositories {
    mavenCentral()
}

def TEAM = 865

def PACKAGE_BASE = "ca.warp7"
def PACKAGE_2017_V2 = PACKAGE_BASE + ".frc2017_v2"
def PACKAGE_2018 = PACKAGE_BASE + ".frc2018"

def ROBOT_PACKAGE = PACKAGE_2018
def ROBOT_CLASS_NAME = "Robot"
def ROBOT_CLASS = ROBOT_PACKAGE + "." + ROBOT_CLASS_NAME


deploy {
    targets {
        target("roborio", RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', FRCJavaArtifact) {
            //noinspection GroovyAssignabilityCheck
            targets << "roborio"
            debug = false
        }
    }
}

// Override the default version for the libraries
// because they are not updated often

wpi {
    wpilibVersion = "2018.4.1"
    wpiutilVersion = "3.2.0"
    ctreVersion = '5.3.1.0'
    ntcoreVersion = "4.1.0"
    cscoreVersion = "1.3.0"
    shuffleboardVersion = '1.3.1'
    smartDashboardVersion = "3.0.1"
}

dependencies {
    compile fileTree(dir: '../libs', include: ['*.jar'])

    //noinspection GrUnresolvedAccess,GroovyAssignabilityCheck
    compile wpilib()

    if (ROBOT_PACKAGE == PACKAGE_2017_V2) {

        //noinspection GrUnresolvedAccess,GroovyAssignabilityCheck
        compile ctreLegacy()

    } else {

        //noinspection GrUnresolvedAccess,GroovyAssignabilityCheck
        compile ctre()

    }

    //noinspection GrUnresolvedAccess,GroovyAssignabilityCheck
    compile navx()

    //compile "org.la4j:la4j:+" // Linear Algebra Library
}

sourceSets {
    main {
        java {
            if (ROBOT_PACKAGE == PACKAGE_2017_V2){
                exclude "ca/warp7/frc2017_v2/subsystems/Shooter.java"
            } else {
                exclude "ca/warp7/frc2017_v2/*"
            }
        }
    }
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}
